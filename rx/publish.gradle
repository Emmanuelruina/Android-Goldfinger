apply plugin: "maven-publish"
apply plugin: "signing"

task sourcesJar(type: Jar, dependsOn: assemble) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}

// Sonatype will not accept a published artifact without at least an empty Javadoc JAR.
task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name sonatype.name()
                url sonatype.url()
                credentials {
                    username sonatype.username()
                    password sonatype.password()
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = releaseConfig.groupId
                version = versions.goldfinger

                artifactId = 'goldfinger-rx'

                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocsJar

                pom {
                    name = 'Goldfinger'
                    description = 'Android library to simplify Biometric authentication implementation.'
                    url = 'https://github.com/infinum/Android-Goldfinger'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'Aksi0m'
                            name = 'Renato Turic'
                            email = 'renato.turic@infinum.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/infinum/Android-Goldfinger.git'
                        developerConnection = 'https://github.com/infinum/Android-Goldfinger.git'
                        url = 'https://github.com/infinum/Android-Goldfinger'
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                signing {
                    sign publishing.publications.release
                }
            }
        }
    }
}